<?xml version = "1.0" encoding = "utf-8" ?>

<!--********** Copyright 2016 Roku Corp.  All Rights Reserved. **********-->

<component name = "AudioListExample" extends = "Group" >

  <script type = "text/brightscript" >

    <![CDATA[

    sub init()
      m.audiolist = m.top.findNode("audioLabelList")

      m.audiolist.observeField("itemFocused", "setaudio")
      m.audiolist.observeField("itemSelected", "playaudio")

      m.audio = createObject("RoSGNode", "Audio")
      m.audio.observeField("state","controlaudioplay")

      m.readAudioContentTask = createObject("RoSGNode", "ContentReader")
      m.readAudioContentTask.observeField("content", "showaudiolist")
      m.readAudioContentTask.contenturi = "http://www.sdktestinglab.com/Tutorial/content/audiocontent.xml"
      m.readAudioContentTask.control = "RUN" 
    end sub

    sub showaudiolist()
      m.audiolist.content = m.readAudioContentTask.content
      m.audiolist.setFocus(true)
    end sub

    sub setaudio()
      audiocontent = m.audiolist.content.getChild(m.audiolist.itemFocused)
      m.audio.content = audiocontent
    end sub

    sub playaudio()
      m.audio.control = "stop"
      m.audio.control = "none"
      m.audio.control = "play"
    end sub

    sub controlaudioplay()
      if (m.audio.state = "finished") 
        m.audio.control = "stop"
        m.audio.control = "none"
      end if
    end sub

    function onKeyEvent(key as String, press as Boolean) as Boolean
      if press then
        if key = "back"
          if (m.audio.state = "playing")
            m.audio.control = "stop"

            return true
          end if
        end if
      end if

      return false
    end function

    ]]>

  </script>

  <children >

    <LabelList 
      id = "audioLabelList" 
      translation = "[ 140, 100 ]" 
      itemSize = "[ 350, 60 ]" />

    <Poster 
      uri = "pkg:/images/audio_hd.jpg" 
      translation = "[ 560, 100 ]" 
      width = "494" 
      height = "574" />

  </children>

</component>
