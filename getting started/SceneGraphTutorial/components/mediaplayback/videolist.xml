<?xml version = "1.0" encoding = "utf-8" ?>

<!--********** Copyright 2016 Roku Corp.  All Rights Reserved. **********-->

<component name = "VideoListExample" extends = "Group" >

  <script type = "text/brightscript" >

    <![CDATA[

    sub init()
      m.videolist = m.top.findNode("videoLabelList")
      m.videoinfo = m.top.findNode("infoLabel")
      m.videoposter = m.top.findNode("videoPoster")
      m.video = m.top.findNode("exampleVideo")

      m.videolist.observeField("itemFocused", "setvideo")
      m.videolist.observeField("itemSelected", "playvideo")

      m.video.observeField("state","controlvideoplay")

      m.readVideoContentTask = createObject("RoSGNode", "ContentReader")
      m.readVideoContentTask.observeField("content", "showvideolist")
      m.readVideoContentTask.contenturi = "http://www.sdktestinglab.com/Tutorial/content/videocontent.xml"
      m.readVideoContentTask.control = "RUN"
    end sub

    sub showvideolist()
      m.videolist.content = m.readVideoContentTask.content
      m.videolist.setFocus(true)
    end sub

    sub setvideo()
      videocontent = m.videolist.content.getChild(m.videolist.itemFocused)
      m.videoposter.uri = videocontent.hdposterurl
      m.videoinfo.text = videocontent.description
      m.video.content = videocontent
    end sub

    sub playvideo()
      m.video.control = "play"
      m.video.visible = true
      m.video.setFocus(true)
    end sub

    sub controlvideoplay()
      if (m.video.state = "finished") 
        m.video.control = "stop"
        m.videolist.setFocus(true)
        m.video.visible = false
      end if
    end sub

    function onKeyEvent(key as String, press as Boolean) as Boolean
      if press then
        if key = "back"
          if (m.video.state = "playing")
            m.video.control = "stop"
            m.videolist.setFocus(true)
            m.video.visible = false

            return true
          end if
        end if
      end if

      return false
    end function

    ]]>

  </script>

  <children >

    <LabelList 
      id = "videoLabelList" 
      translation = "[ 140, 100 ]"
      itemSize = "[ 350, 60 ]" />

    <Poster 
      id = "tvPoster" 
      translation = "[ 560, 100 ]" 
      width = "647" 
      height = "448" 
      uri = "pkg:/images/tv_screen_hd.png" >

      <Poster 
        id = "videoPoster" 
        translation = "[ 53, 45 ]" 
        width = "540" 
        height = "304" />

      <Rectangle 
        id = "infoRectangle" 
        translation = "[ 0, 460 ]" 
        width = "647" 
        height = "76" 
        color = "0x101010FF" >

        <Label 
          id = "infoLabel" 
          translation = "[ 20, 20 ]" 
          width = "607" 
          height = "36" 
          font = "font:LargeBoldSystemFont" 
          horizalign = "center" 
          vertalign = "center" 
          wrap = "true" />

      </Rectangle>

    </Poster>

    <Video 
      id = "exampleVideo" 
      visible = "false" />

  </children>

</component>
